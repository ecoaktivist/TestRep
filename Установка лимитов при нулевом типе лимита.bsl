#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПослеЗаписиВерсийИсторииДанных(ИнформацияОЗаписиВерсий)
	
	Для Каждого ВерсияИстории Из ИнформацияОЗаписиВерсий Цикл
		Попытка
			Коэффициенты = афлРасчетЛимитовСервер.РассчитатьКоэффициенты(ВерсияИстории.Данные.Владелец,
				ВерсияИстории.Данные.СтруктураСобственности, ВерсияИстории.Данные.ПризнакНаличияИсков);
			
			ДопДанные = Новый Структура;
			ДопДанные.Вставить("СтруктураСобственности", ВерсияИстории.Данные.СтруктураСобственности);
			ДопДанные.Вставить("КоэффициентСтруктурыСобственности",
				ВерсияИстории.Данные.СтруктураСобственности.КоэффициентСтруктурыСобственности);
			ДопДанные.Вставить("ПризнакНаличияИсков", ВерсияИстории.Данные.ПризнакНаличияИсков);
			
			афлРасчетЛимитовСервер.ЗаписатьДанныеВРегистр(ВерсияИстории.Данные, ВерсияИстории.НомерВерсии,
				ВерсияИстории.Дата, Коэффициенты, ВерсияИстории.ДатаПервогоДоговора, ДопДанные);
			ИсторияДанных.УдалитьИзОбработкиПослеЗаписиВерсий(ВерсияИстории.Данные);
			
			ИзменилсяТипЛимита = ВерсияИстории.РазличияВерсий.Свойство("ТипЛимита");
			
			Если ИзменилсяТипЛимита Тогда
				
				Данные 		= ВерсияИстории.ДанныеВерсии;
				ТипЛимита 	= Данные.ТипЛимита.Ссылка;
				
				Если ТипЛимита = ПредопределенноеЗначение("Справочник.афлТипыЛимитов.НулевойЛимит") Тогда
					СоздаемДокументУстановкаЛимитовДЗАвансирования(Данные);
				КонецЕсли;
				
			КонецЕсли;
			
		Исключение
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздаемДокументУстановкаЛимитовДЗАвансирования(Данные);
	
	Документ = Документы.афлУстановкаЛимитовДЗАвансирования.СоздатьДокумент();
	
	Документ.ВидЛимита 					= ПредопределенноеЗначение("Перечисление.афлВидыЛимитов.ЛимитДЗ");
	Документ.ДатаНачала					= ТекущаяДатаСеанса();
	Документ.Контрагент 				= Данные.Владелец.Ссылка;
	Документ.КредитнаяГруппаКонтрагента = Данные.КредитнаяГруппаКонтрагентов.Ссылка;
	Документ.ПККДЗ 						= Данные.ПККДЗ.Ссылка;
	Документ.ТипЛимита 					= Данные.ТипЛимита.Ссылка;
	Документ.Исполнитель				= Пользователи.ТекущийПользователь();
	Документ.ЛимитДЗ 					= 0;
	
	Документ.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли